@Library('jenkins-pipeline-shared-libraries')_

deployProperties = [:]

pipeline {
    agent {
        label 'kogito-operator-slave && !master'
    }

    // Needed for local build
    tools {
        jdk 'kie-jdk11'
    }

    options {
        buildDiscarder logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '', numToKeepStr: '10')
        timeout(time: 420, unit: 'MINUTES')
    }

    parameters {
        string(name: 'DISPLAY_NAME', defaultValue: '', description: 'Setup a specific build display name')
        
        // Git information
        string(name: 'BUILD_BRANCH_NAME', defaultValue: 'master', description: 'Which branch to build ? Set if you are not on a multibranch pipeline.')
        string(name: 'GIT_AUTHOR', defaultValue: 'kiegroup', description: 'Which Git author repository ?')

        // Build&Test information
        booleanParam(name: 'SMOKE_TESTS_ONLY', defaultValue: false, description: 'If only smoke tests should be run. Default is full testing.')
        booleanParam(name: 'SKIP_TESTS', defaultValue: false, description: 'Skip tests')
        string(name: 'MAVEN_ARTIFACT_REPOSITORY', defaultValue: '', description: 'Maven repository where the build artifacts are present')

        // Deploy information
        booleanParam(name: 'IMAGE_USE_OPENSHIFT_REGISTRY', defaultValue: false, description: 'Set to true if image should be deployed in Openshift registry.In this case, IMAGE_REGISTRY_CREDENTIALS, IMAGE_REGISTRY and IMAGE_NAMESPACE parameters will be ignored')
        string(name: 'IMAGE_REGISTRY_CREDENTIALS', defaultValue: '', description: 'Image registry credentials to use to deploy images. Will be ignored if no IMAGE_REGISTRY is given')
        string(name: 'IMAGE_REGISTRY', defaultValue: '', description: 'Image registry to use to deploy images')
        string(name: 'IMAGE_NAMESPACE', defaultValue: 'kiegroup', description: 'Image namespace to use to deploy images')
        string(name: 'IMAGE_NAME_SUFFIX', defaultValue: '', description: 'Image name suffix to use to deploy images. In case you need to change the final image name, you can add a suffix to it.')
        string(name: 'IMAGE_TAG', defaultValue: '', description: 'Image tag to use to deploy images')

        // Test config if needed specifics. Else test default config will apply.
        booleanParam(name: 'KOGITO_IMAGES_IN_OPENSHIFT_REGISTRY', defaultValue: false, description: 'Set to true if kogito images for tests are in internal Openshift registry.In this case, KOGITO_IMAGES_REGISTRY and KOGITO_IMAGES_NAMESPACE parameters will be ignored')
        string(name: 'KOGITO_IMAGES_REGISTRY', defaultValue: '', description: 'Test images registry')
        string(name: 'KOGITO_IMAGES_NAMESPACE', defaultValue: '', description: 'Test images namespace')
        string(name: 'KOGITO_IMAGES_NAME_SUFFIX', defaultValue: '', description: 'Test images name suffix')
        string(name: 'KOGITO_IMAGES_TAG', defaultValue: '', description: 'Test images tag')
        string(name: 'EXAMPLES_URI', defaultValue: '', description: 'Git uri to the kogito-examples repository to use for tests.')
        string(name: 'EXAMPLES_REF', defaultValue: '', description: 'Git reference (branch/tag) to the kogito-examples repository to use for tests.')

        // Release information
        booleanParam(name: 'RELEASE', defaultValue: false, description: 'Is this build for a release?')
        string(name: 'PROJECT_VERSION', defaultValue: '', description: 'Optional if not RELEASE. If RELEASE, cannot be empty.')
    }

    environment {
        JAVA_HOME = "${GRAALVM_HOME}"
        
        OPERATOR_IMAGE_NAME="kogito-cloud-operator"

        OPENSHIFT_API = credentials("OPENSHIFT_API")
        OPENSHIFT_REGISTRY = credentials("OPENSHIFT_REGISTRY")
        OPENSHIFT_INTERNAL_REGISTRY = "image-registry.openshift-image-registry.svc:5000"
        OPENSHIFT_CREDS_KEY = "OPENSHIFT_CREDS"

        BOT_CREDENTIALS_ID = "bsig-gh-bot"
    }
    
    stages {
        stage('Initialize') {
            steps {
                script {
                    cleanWs()

                    // Make sure Openshift is available and can authenticate before continuing
                    loginOpenshift()

                    if (params.DISPLAY_NAME != "") {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    checkoutRepo()

                    if (isRelease()) {
                        assert getProjectVersion() != ''
                    }

                    // Add some params to properties
                    setDeployPropertyIfNeeded('git.branch', getBuildBranch())
                    setDeployPropertyIfNeeded('git.author', getGitAuthor())
                    setDeployPropertyIfNeeded('project.version', getProjectVersion())
                    setDeployPropertyIfNeeded('release', "${isRelease()}")
                }
            }
        }
        stage('Prepare for PR'){
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    githubscm.forkRepo(env.BOT_CREDENTIALS_ID)
                    githubscm.createBranch(getProjectVersion())
                }
            }
        }
        stage('Update version') {
            when {
                expression { return getProjectVersion() != '' }
            }
            steps {
                sh "./hack/bump-version.sh ${getProjectVersion()} ${isRelease()}"
            }
        }
        stage('Build Kogito Operator') {
            steps {
                sh """
                    go get -u golang.org/x/lint/golint
                    make image_builder=podman
                """
            }
        }
        stage('Build Kogito CLI') {
            steps {
                script {
                    // Dependency needed to pack some files in CLI binary
                    sh "go get -u github.com/gobuffalo/packr/v2/packr2"
                    
                    // Build the CLI
                    sh "make build-cli"
                }
            }
            post {
                success {
                    script {
                        archiveArtifacts artifacts: "build/_output/bin/kogito", allowEmptyArchive: false
                    }
                }
            }
        }
        stage('Push Operator Image to Openshift Registry') {
            steps {
                loginOpenshiftRegistry()
                sh """
                    podman tag quay.io/kiegroup/${OPERATOR_IMAGE_NAME}:${getOperatorVersion()} ${buildTempOpenshiftImageFullName()}
                    podman push --tls-verify=false ${buildTempOpenshiftImageFullName()}
                """
            }
        }
        stage("Build examples' images for testing"){
            when {
                expression {
                    return shouldLaunchTests()
                }
            }
            steps {
                script {
                    // Need to login to Openshift Registry for application images to be pushed
                    loginOpenshiftRegistry()

                    try {
                        // Optaplanner taking a lot of resources, we should not build in parallel
                        sh "make build-examples-images concurrent=1 ${getBDDParameters('never', false)}"
                    } catch(err) {
                        unstable("Error building examples' images. Check the junit results.")
                    }
                }
            }
            post {
                always {
                    archiveArtifacts artifacts: 'test/logs/**/*.log', allowEmptyArchive: true
                    junit testResults: 'test/logs/**/junit.xml', allowEmptyResults: true
                    sh "cd test && go run scripts/prune_namespaces.go"
                }
            }
        }
        stage('Run Full Testing') {
            when {
                expression { return shouldLaunchTests() }
            }
            steps {
                script {
                    // Catch and set unstable so the temp image is still pushed and we get the deployment properties, 
                    // in case we decide to continue in the release
                    try {
                        sh "make run-tests timeout=360 load_factor=3 concurrent=3 smoke=${params.SMOKE_TESTS_ONLY} ${getBDDParameters('always', true)}"
                    } catch(err) {
                        unstable("Tests are failing")
                    }
                }
            }
            post {
                always {
                    archiveArtifacts artifacts: 'test/logs/**/*.log', allowEmptyArchive: true
                    junit testResults: 'test/logs/**/junit.xml', allowEmptyResults: true
                    sh "cd test && go run scripts/prune_namespaces.go"
                }
            }
        }
        stage('Push to registry') {
            steps {
                script {
                    if (isDeployImageInOpenshiftRegistry()) {
                        loginOpenshiftRegistry()
                    } else if (getDeployImageRegistryCredentials() != ''){
                        loginContainerRegistry(getDeployImageRegistry(), getDeployImageRegistryCredentials())
                    }
                    tagImage()
                    pushImage()
                }
            }
            post {
                success {
                    script {
                        // Store image deployment information
                        String imgPrefix = "kogito-cloud-operator.image"
                        setDeployPropertyIfNeeded("${imgPrefix}.registry", getDeployImageRegistry())
                        setDeployPropertyIfNeeded("${imgPrefix}.namespace", getDeployImageNamespace())
                        setDeployPropertyIfNeeded("${imgPrefix}.name-suffix", getDeployImageNameSuffix())
                        setDeployPropertyIfNeeded("${imgPrefix}.tag", getDeployImageTag())
                    }
                }
            }
        }
        stage('Create PR'){
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    String commitMsg = "Update project version to ${getProjectVersion()} for release"
                    String prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}"
                    githubscm.commitChanges(commitMsg, ".")
                    githubscm.pushObject("origin", getProjectVersion(), env.BOT_CREDENTIALS_ID)
                    deployProperties["kogito-cloud-operator.pr.link"] = githubscm.createPR(commitMsg, prBody, getBuildBranch(), env.BOT_CREDENTIALS_ID)
                }
            }
        }
    }
    post {
        always {
            script {
                def propertiesStr = deployProperties.collect{ entry ->  "${entry.key}=${entry.value}" }.join("\n")
                writeFile(text: propertiesStr, file: "deployment.properties")
                archiveArtifacts(artifacts: "deployment.properties")
            }
            cleanWs()
        }
    }
}

void checkoutRepo() {
    deleteDir()
    checkout(githubscm.resolveRepository("kogito-cloud-operator", getGitAuthor(), getBuildBranch(), false))
}

String getOperatorVersion(){
    return sh(script: 'source ./hack/export-version.sh > /dev/null && echo ${OP_VERSION}', returnStdout: true).trim()
}

void tagImage(){
    sh "podman tag ${buildTempOpenshiftImageFullName()} ${buildFinalImageFullName()}"
}

void pushImage(){
    sh "podman push --tls-verify=false ${buildFinalImageFullName()}"
}

String buildTempOpenshiftImageFullName(boolean internal=false){
    return "${getTempOpenshiftImageName(internal)}:${getTempTag()}"
}
String getTempOpenshiftImageName(boolean internal=false){
    String registry = internal ? env.OPENSHIFT_INTERNAL_REGISTRY : env.OPENSHIFT_REGISTRY
    return "${registry}/openshift/${OPERATOR_IMAGE_NAME}"
}

String buildFinalImageFullName(){
    String finalImageName = env.OPERATOR_IMAGE_NAME
    if(getDeployImageNameSuffix() != ''){
        finalImageName += "-" + getDeployImageNameSuffix()
    }
    return "${getDeployImageRegistry()}/${getDeployImageNamespace()}/${finalImageName}:${getDeployImageTag()}"
}

void loginOpenshift(){
    withCredentials([usernamePassword(credentialsId: env.OPENSHIFT_CREDS_KEY, usernameVariable: 'OC_USER', passwordVariable: 'OC_PWD')]){
        sh "oc login --username=${OC_USER} --password=${OC_PWD} --server=${OPENSHIFT_API} --insecure-skip-tls-verify"
    }
}

void loginOpenshiftRegistry(){
    loginOpenshift()
    // username can be anything. See https://docs.openshift.com/container-platform/4.4/registry/accessing-the-registry.html#registry-accessing-directly_accessing-the-registry
    sh "set +x && podman login -u anything -p \$(oc whoami -t) --tls-verify=false ${env.OPENSHIFT_REGISTRY}"
}

void loginContainerRegistry(String registry, String credsId){
    withCredentials([usernamePassword(credentialsId: credsId, usernameVariable: 'REGISTRY_USER', passwordVariable: 'REGISTRY_PWD')]) {
        sh "podman login -u ${REGISTRY_USER} -p ${REGISTRY_PWD} --tls-verify=false ${registry}"
    }
}

String getTempTag(){
    hash = sh(returnStdout: true,script: 'git rev-parse --short HEAD').trim()
    return "pr-${hash}"
}

void setDeployPropertyIfNeeded(String key, def value){
    if (value != null && value != ""){
        deployProperties[key] = value
    }
}

////////////////////////////////////////////////////////////////////////
// Deploy image information
////////////////////////////////////////////////////////////////////////

boolean isDeployImageInOpenshiftRegistry(){
    return params.IMAGE_USE_OPENSHIFT_REGISTRY
}

String getDeployImageRegistryCredentials(){
    return isDeployImageInOpenshiftRegistry() ? "" : params.IMAGE_REGISTRY_CREDENTIALS
}

String getDeployImageRegistry(){
    return isDeployImageInOpenshiftRegistry() ? env.OPENSHIFT_REGISTRY : params.IMAGE_REGISTRY
}

String getDeployImageNamespace(){
    return isDeployImageInOpenshiftRegistry() ? "openshift" : params.IMAGE_NAMESPACE
}

String getDeployImageNameSuffix(){
    return params.IMAGE_NAME_SUFFIX
}

String getDeployImageTag(){
    if (params.IMAGE_TAG != ""){
        return params.IMAGE_TAG
    } else {
        sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
    }
}

////////////////////////////////////////////////////////////////////////
// Utils
////////////////////////////////////////////////////////////////////////

boolean isRelease() {
    return params.RELEASE
}

String getBuildBranch(){
    return params.BUILD_BRANCH_NAME
}

String getGitAuthor(){
    return params.GIT_AUTHOR
}

String getProjectVersion(){
    return params.PROJECT_VERSION
}

boolean shouldLaunchTests(){
    return !params.SKIP_TESTS
}

////////////////////////////////////////////////////////////////////////
// BDD parameters
////////////////////////////////////////////////////////////////////////

String getBDDParameters(String image_cache_mode, boolean runtime_app_registry_internal=false) {
    testParamsMap = [:]

    testParamsMap["load_default_config"] = true
    testParamsMap["ci"] = "jenkins"

    testParamsMap["operator_image"] = getTempOpenshiftImageName(true)
    testParamsMap["operator_tag"] = getTempTag()

    if (params.MAVEN_ARTIFACT_REPOSITORY != '') {
        // No mirror if we set directly the Maven repository
        // Tests will be slower but we need to test against specific artifacts
        testParamsMap["custom_maven_repo"] = params.MAVEN_ARTIFACT_REPOSITORY
    } else if(env.MAVEN_MIRROR_REPOSITORY != null
                && env.MAVEN_MIRROR_REPOSITORY != '') {
        testParamsMap["maven_mirror"] = env.MAVEN_MIRROR_REPOSITORY
    }

    if (params.KOGITO_IMAGES_IN_OPENSHIFT_REGISTRY) {
        testParamsMap["build_image_registry"] = env.OPENSHIFT_INTERNAL_REGISTRY
        testParamsMap["services_image_registry"] = env.OPENSHIFT_INTERNAL_REGISTRY
        testParamsMap["build_image_namespace"] = "openshift"
        testParamsMap["services_image_namespace"] = "openshift"
    } else {
        if(params.KOGITO_IMAGES_REGISTRY != '') {
            testParamsMap["build_image_registry"] = params.KOGITO_IMAGES_REGISTRY
            testParamsMap["services_image_registry"] = params.KOGITO_IMAGES_REGISTRY
        }
        if(params.KOGITO_IMAGES_NAMESPACE != '') {
            testParamsMap["build_image_namespace"] = params.KOGITO_IMAGES_NAMESPACE
            testParamsMap["services_image_namespace"] = params.KOGITO_IMAGES_NAMESPACE
        }
    }
    // runtime_application_image are built in this pipeline so we can just use Openshift registry for them
    testParamsMap["image_cache_mode"] = image_cache_mode
    testParamsMap["runtime_application_image_registry"] = runtime_app_registry_internal ? env.OPENSHIFT_INTERNAL_REGISTRY : env.OPENSHIFT_REGISTRY
    testParamsMap["runtime_application_image_namespace"] = "openshift"

    if(params.KOGITO_IMAGES_NAME_SUFFIX != '') {
        testParamsMap["build_image_name_suffix"] = params.KOGITO_IMAGES_NAME_SUFFIX
        testParamsMap["services_image_name_suffix"] = params.KOGITO_IMAGES_NAME_SUFFIX
        testParamsMap["runtime_application_image_name_suffix"] = params.KOGITO_IMAGES_NAME_SUFFIX
    }

    if(params.KOGITO_IMAGES_TAG != '') {
        testParamsMap["build_image_version"] = params.KOGITO_IMAGES_TAG
        testParamsMap["services_image_version"] = params.KOGITO_IMAGES_TAG
        testParamsMap["runtime_application_image_version"] = params.KOGITO_IMAGES_TAG
    }

    if(params.EXAMPLES_REF != '') {
        // Override examples_ref as there is a current problem on release pipeline ...
        if(isRelease()){
            testParamsMap["examples_ref"] = getProjectVersion()
        } else {
            testParamsMap["examples_ref"] = params.EXAMPLES_REF
        }
    }
    if(params.EXAMPLES_URI != '') {
        testParamsMap["examples_uri"] = params.EXAMPLES_URI
    }
    
    // Use podman container engine in tests
    testParamsMap['container_engine'] = 'podman'

    String testParams = testParamsMap.collect{ entry -> "${entry.getKey()}=\"${entry.getValue()}\"" }.join(" ")
    echo "BDD parameters = ${testParams}"
    return testParams
}

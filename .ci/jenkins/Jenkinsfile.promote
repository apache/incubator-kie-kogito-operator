@Library('jenkins-pipeline-shared-libraries')_

IMAGES = ['kogito-operator']

helper = null

pipeline {
    agent {
        label 'rhel8 && podman && !built-in'
    }

    options {
        timeout(time: 120, unit: 'MINUTES')
    }

    environment {
        KOGITO_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")

        CONTAINER_ENGINE = 'podman'
        CONTAINER_TLS_OPTIONS = '--tls-verify=false'

        PR_BRANCH_HASH = "${util.generateHash(10)}"
        GITHUB_REPO = "${REPO_NAME}" // for github-release cli
    }

    stages {
        stage('Setup pipeline') {
            steps {
                script {
                    helper = load '.ci/jenkins/scripts/helper.groovy'
                    helper.initPipeline()
                }
            }
        }
        stage('Initialize') {
            steps {
                script {
                    helper.cleanGoPath()

                    helper.updateDisplayName()

                    properties.readFromUrl(helper.getDeployPropertiesFileUrl())

                    assert helper.getProjectVersion()
                    assert helper.getBuildBranch() == util.getReleaseBranchFromVersion(helper.getProjectVersion())
                }
            }
        }
        stage('Pull "old" images') {
            steps {
                script {
                    helper.loginRegistry(baseImageParamsPrefix)

                    for (String imageName : IMAGES) {
                        container.pullImage(helper.getImageFullTag(imageName, baseImageParamsPrefix))
                    }
                }
            }
        }
        stage('Tag images') {
            steps {
                script {
                    for (String imageName : IMAGES) {
                        container.tagImage(helper.getImageFullTag(imageName, baseImageParamsPrefix), helper.getImageFullTag(imageName, promoteImageParamsPrefix))
                        if (helper.isDeployLatestTag()) {
                            container.tagImage(helper.getImageFullTag(imageName, baseImageParamsPrefix), helper.getImageFullTag(imageName, promoteImageParamsPrefix, 'latest'))
                        }
                    }
                }
            }
        }
        stage('Pushing') {
            steps {
                script {
                    helper.loginRegistry(promoteImageParamsPrefix)

                    for (String imageName : IMAGES) {
                        container.pushImage(helper.getImageFullTag(imageName, promoteImageParamsPrefix))
                        if (helper.isDeployLatestTag()) {
                            container.pushImage(helper.getImageFullTag(imageName, promoteImageParamsPrefix, 'latest'))
                        }

                        if (helper.getImageRegistry() == 'quay.io') {
                            helper.makeQuayImagePublic(helper.getFullImageName(imageName))
                        }
                    }
                }
            }
        }

        stage('Merge PR, tag and release CLI') {
            steps {
                script {
                    dir(helper.getRepoName()) {
                        helper.checkoutRepo()

                        // Merge PR
                        String prLink = properties.retrieve("${helper.getRepoName()}.pr.link")
                        if (prLink) {
                            githubscm.mergePR(prLink, helper.getGitAuthorCredsID())
                            githubscm.pushObject('origin', helper.getBuildBranch(), helper.getGitAuthorCredsID())
                        }
                    }
                }
            }
        }

        stage('Create release') {
            steps {
                script {
                    dir(helper.getRepoName()) {
                        if (githubscm.isReleaseExist(helper.getGitTag(), helper.getGitAuthorCredsID())) {
                            githubscm.deleteReleaseAndTag(helper.getGitTag(), helper.getGitAuthorCredsID())
                        }
                        githubscm.createReleaseWithGeneratedReleaseNotes(helper.getGitTag(), helper.getBuildBranch(), githubscm.getPreviousTagFromVersion(helper.getGitTag(), 'v'), helper.getGitAuthorCredsID())
                        githubscm.updateReleaseBody(helper.getGitTag(), helper.getGitAuthorCredsID())
    
                        sh "make build-cli release=true version=${helper.getProjectVersion()}"
                        def releasePath = 'build/_output/release/'
                        def cliBaseName = "kogito-cli-${helper.getProjectVersion()}"
                        def darwinFileName = "${cliBaseName}-darwin-amd64.tar.gz"
                        def linuxFileName = "${cliBaseName}-linux-amd64.tar.gz"
                        def windowsFileName = "${cliBaseName}-windows-amd64.zip"
                        def yamlInstaller = 'kogito-operator.yaml'
                        withCredentials([usernamePassword(credentialsId: helper.getGitAuthorCredsID(), usernameVariable: 'GH_USER', passwordVariable: 'GH_TOKEN')]) {
                            sh """
                                gh release upload ${helper.getGitTag()} "${releasePath}${darwinFileName}"
                                gh release upload ${helper.getGitTag()} "${releasePath}${linuxFileName}"
                                gh release upload ${helper.getGitTag()} "${releasePath}${windowsFileName}"
                                gh release upload ${helper.getGitTag()} "${yamlInstaller}"
                            """
                        }
                    }
                }
            }
        }
    }
    post {
        unsuccessful {
            sendNotification()
        }
        cleanup {
            script {
                helper.cleanGoPath()
                util.cleanNode(containerEngine)
            }
        }
    }
}

void sendNotification() {
    if (params.SEND_NOTIFICATION) {
        mailer.sendMarkdownTestSummaryNotification('Promote', "[${helper.getBuildBranch()}] Kogito Operator", [env.KOGITO_CI_EMAIL_TO])
    } else {
        echo 'No notification sent per configuration'
    }
}

@Library('jenkins-pipeline-shared-libraries')_

helper = null

pipeline {
    agent {
        label 'rhel8 && podman && !built-in'
    }

    options {
        timeout(time: 120, unit: 'MINUTES')
    }

    environment {
        KOGITO_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")

        CONTAINER_ENGINE = 'podman'
        CONTAINER_TLS_OPTIONS = '--tls-verify=false'

        BOT_BRANCH_HASH = "${util.generateHash(10)}"
        GITHUB_REPO = "${REPO_NAME}" // for github-release cli
    }

    stages {
        stage('Setup pipeline') {
            steps {
                script {
                    helper = load '.ci/jenkins/scripts/helper.groovy'
                    helper.initPipeline()
                }
            }
        }
        stage('Initialize') {
            steps {
                script {
                    helper.cleanGoPath()
                    
                    helper.updateDisplayName()

                    properties.readFromUrl(helper.getDeployPropertiesFileUrl())

                    if (helper.isRelease()) {
                        // Verify version is set and if on right release branch
                        assert helper.getProjectVersion()

                        assert helper.getBuildBranch() == util.getReleaseBranchFromVersion(helper.getProjectVersion())
                    }
                }
            }
        }
        stage('Pull "old" images') {
            steps {
                script {
                    helper.loginRegistry(baseImageParamsPrefix)

                    for (String imageName : getImages()) {
                        container.pullImage(helper.getImageFullTag(imageName, baseImageParamsPrefix))
                    }
                }
            }
        }
        stage('Tag images') {
            steps {
                script {
                    for (String imageName : getImages()) {
                        container.tagImage(helper.getImageFullTag(imageName, baseImageParamsPrefix), helper.getImageFullTag(imageName, promoteImageParamsPrefix))
                        if (helper.isDeployLatestTag()) {
                            container.tagImage(helper.getImageFullTag(imageName, baseImageParamsPrefix), helper.getImageFullTag(imageName, promoteImageParamsPrefix, 'latest'))
                        }
                        String reducedTag = helper.getReducedTag(promoteImageParamsPrefix)
                        if (reducedTag) {
                            container.tagImage(helper.getImageFullTag(imageName, baseImageParamsPrefix), helper.getImageFullTag(imageName, promoteImageParamsPrefix, reducedTag))
                        }
                    }
                }
            }
        }
        stage('Pushing') {
            steps {
                script {
                    helper.loginRegistry(promoteImageParamsPrefix)

                    for (String imageName : getImages()) {
                        container.pushImage(helper.getImageFullTag(imageName, promoteImageParamsPrefix))
                        if (helper.isDeployLatestTag()) {
                            container.pushImage(helper.getImageFullTag(imageName, promoteImageParamsPrefix, 'latest'))
                        }
                        String reducedTag = helper.getReducedTag(promoteImageParamsPrefix)
                        if (reducedTag) {
                            container.pushImage(helper.getImageFullTag(imageName, promoteImageParamsPrefix, reducedTag))
                        }

                        if (helper.getImageRegistry(promoteImageParamsPrefix) == 'quay.io') {
                            helper.makeQuayImagePublic(helper.getFullImageName(imageName, promoteImageParamsPrefix), promoteImageParamsPrefix)
                        }
                    }
                }
            }
        }
    }
    post {
        unsuccessful {
            sendNotification()
        }
        cleanup {
            script {
                helper.cleanGoPath()
                util.cleanNode(containerEngine)
            }
        }
    }
}

void sendNotification() {
    if (params.SEND_NOTIFICATION) {
        mailer.sendMarkdownTestSummaryNotification('Promote', "[${helper.getBuildBranch()}] Kogito Examples Images", [env.KOGITO_CI_EMAIL_TO])
    } else {
        echo 'No notification sent per configuration'
    }
}

List getImages() {
    return helper.getImageNames(baseImageParamsPrefix)
}

@Library('jenkins-pipeline-shared-libraries')_

helper = null

pipeline {
    agent {
        label 'kogito-operator-slave && !master'
    }

    // Needed for local build
    tools {
        jdk 'kie-jdk11'
    }

    options {
        timeout(time: 360, unit: 'MINUTES')
    }

    // parameters {
    // For parameters, check into .jenkins/dsl/jobs.groovy file
    // }

    environment {
        // Static env is defined into .jenkins/dsl/jobs.groovy file

        JAVA_HOME = "${GRAALVM_HOME}"
    }

    stages {
        stage('Setup pipeline') {
            steps {
                script {
                    helper = load '.jenkins/scripts/helper.groovy'
                    helper.initPipeline()
                }
            }
        }
        stage('Initialize') {
            steps {
                script {
                    helper.updateDisplayName()
                    helper.checkoutRepo()

                    // Openshift login is needed for the examples generation
                    openshift.loginOpenshift()

                    helper.loginRegistry()
                }
            }
            post {
                success {
                    script {
                        properties.add('git.branch', helper.getBuildBranch())
                        properties.add('git.author', helper.getGitAuthor())
                    }
                }
            }
        }

        stage('Build non native examples images') {
            steps {
                sh "make build-examples-images concurrent=3 smoke=${params.SMOKE_TESTS_ONLY} ${getExamplesBuildParameters(false)}"
            }
            post {
                always {
                    archiveArtifacts artifacts: 'test/**/logs/**/*.log', allowEmptyArchive: true
                    junit testResults: 'test/**/logs/**/junit.xml', allowEmptyResults: true
                }
                unsuccesful {
                    unstable("Error building non-native examples' images. Check the junit results.")
                }
            }
        }

        stage('Build native examples images') {
            when {
                expression { return !params.SMOKE_TESTS_ONLY && !params.SKIP_NATIVE_TESTS }
            }
            steps {
                // Optaplanner taking a lot of resources, we should not build in parallel
                sh "make build-examples-images concurrent=1 ${getExamplesBuildParameters(true)}"
            }
            post {
                always {
                    archiveArtifacts artifacts: 'test/**/logs/**/*.log', allowEmptyArchive: true
                    junit testResults: 'test/**/logs/**/junit.xml', allowEmptyResults: true
                }
                unsuccesful {
                    unstable("Error building native examples' images. Check the junit results.")
                }
            }
        }

        stage('Handle built images') {
            steps {
                script {
                    // Retrieve the list of images
                    images = readFile(file: 'test/logs/built_project_image_names.log').split('\n')

                    if (helper.getImageRegistry() == 'quay.io') {
                        // Set visible each repo
                        images.each {
                            helper.makeQuayImagePublic(helper.getFullImageName(it))
                        }
                    }

                    // Store image deployment information
                    properties.add(helper.getImageRegistryProperty(), helper.getImageRegistry())
                    properties.add(helper.getImageNamespaceProperty(), helper.getImageNamespace())
                    properties.add(helper.getImageNamePrefixProperty(), helper.getImageNamePrefix())
                    properties.add(helper.getImageNameSuffixProperty(), helper.getImageNameSuffix())
                    properties.add(helper.getImageNamesProperty(), images.join(','))
                    properties.add(helper.getImageTagProperty(), helper.getImageTag())
                }
            }
        }
    }
    post {
        always {
            script {
                properties.writeToFile(env.PROPERTIES_FILE_NAME)
                archiveArtifacts(artifacts: env.PROPERTIES_FILE_NAME)
            }
        }
        cleanup {
            script {
                util.cleanNode(containerEngine)
            }
        }
    }
}

////////////////////////////////////////////////////////////////////////
// BDD parameters
////////////////////////////////////////////////////////////////////////

String getExamplesBuildParameters(boolean nativeTag = false) {
    Map testParamsMap = helper.getBDDCommonParameters(true)

    testParamsMap['image_cache_mode'] = 'never'
    testParamsMap.putAll(helper.getBDDRuntimeImageParameters())
    
    // May be used by the features ...
    testParamsMap.putAll(helper.getBDDBuildImageParameters('KOGITO_IMAGES'))
    testParamsMap.putAll(helper.getBDDServicesImageParameters('KOGITO_IMAGES'))

    def tags = nativeTag ? [ helper.getNativeTag() ] : [ helper.getNonNativeTag() ]
    if (params.BDD_TEST_TAGS) {
        tags.add(params.BDD_TEST_TAGS)
    }
    testParamsMap['tags'] = tags.join('&&')

    String testParams = testParamsMap.findAll { entry -> entry.getValue() }
                            .collect { entry -> "${entry.getKey()}=\"${entry.getValue()}\"" }.join(' ')
    echo "Example Build parameters = ${testParams}"
    return testParams
}
